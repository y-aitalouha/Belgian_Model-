#TIMEHORIZON
T = 8760;

#NODE METHANOL_PLANTS

// Note that the capacity is in GW, not kt/h.
// In order to compare it with other efuels.
// This node was modeled based on the ENSDK methanol production model, which is in kt/h.
// Thus one has to convert the capacity from kt/h to GW using the lower heating value of methanol (5.51 MWh/tonne).

// Cost COMPUTATION: From ensdk:

// In 2030, 600 ton/day for 138MW
// CAPEX: 1.09M€ per MW --> 6016.8 M€/(kton/h)
// FOM: 30k€/MW per year --> 165.6 M€/y per kt/h meoh

// In 2050, 1200 ton/day for 276MW  
// CAPEX: 0.87M€ per MW --> 4802.4 M€/(kton/h)
// FOM: 26k€/MW per year --> 143.42 M€/y per kt/h meoh


// In GW

#PARAMETERS
pre_installed_capacity = 0.0;
max_capacity = 100;
hhv_methanol = 6.39;
lhv_methanol = 5.51;
lhv_h2 = 33.322;
capex_existing = 4802.4/lhv_methanol;
fom_existing = 143.42/lhv_methanol;
vom_existing = 0/lhv_methanol;
capex = 4802.4/lhv_methanol;
fom = 143.42/lhv_methanol;
vom = 0/lhv_methanol;
lifetime_existing = 30;
lifetime = 30;
conversion_factor_hydrogen = 0.209;
conversion_factor_co2 = 1.37;
conversion_factor_water = 0.59;
conversion_factor_elec = 0.297;
minimum_level = 0;
ramp_rate_up = 1.0;
ramp_rate_down = 1.0; 
outage = 1;
planned_outage = 3 /52;                             // 3 weeks/52       
co2_emission_cost = 80.0 * 1/(10**3);               // M€/kt(co2)

// Others
wacc = 0.07;
nb_year = T/8760;
yearly_capex = capex * wacc / (1 - (1 + wacc)**(-lifetime));
yearly_capex_existing = capex_existing * wacc / (1 - (1 + wacc)**(-lifetime_existing));
yearly_existing_cost = (yearly_capex_existing + fom_existing) * pre_installed_capacity;

#VARIABLES
internal: new_capacity;
external: methanol_produced[T];
external: h2_consumed[T];
external: co2_consumed[T];
// external: co2_produced[T];
external: e_consumed[T];
external: h2o_produced[T];

#CONSTRAINTS
pre_installed_capacity + new_capacity <= max_capacity;
new_capacity >= 0;
methanol_produced[t] >= 0;
h2_consumed[t] >= 0;
co2_consumed[t] >= 0;
// co2_produced[t] >= 0;
h2o_produced[t] >= 0;
e_consumed[t] >= 0;
minimum_level * (pre_installed_capacity + new_capacity) <= methanol_produced[t];
methanol_produced[t] <= pre_installed_capacity + new_capacity;
methanol_produced[i] - methanol_produced[i-1] <= ramp_rate_up * (pre_installed_capacity + new_capacity) for i in [1:T-1];
methanol_produced[i] - methanol_produced[i-1] >= -ramp_rate_down * (pre_installed_capacity + new_capacity) for i in [1:T-1];
outage * sum(methanol_produced[i] for i in [0:T-1]) <= outage * (1 - planned_outage) * (pre_installed_capacity + new_capacity) * T;
h2_consumed[t]/lhv_h2 == conversion_factor_hydrogen * methanol_produced[t]/lhv_methanol;
co2_consumed[t] == conversion_factor_co2 * methanol_produced[t]/lhv_methanol;
h2o_produced[t] == conversion_factor_water * methanol_produced[t]/lhv_methanol;
e_consumed[t] == conversion_factor_elec * methanol_produced[t]/lhv_methanol;


#OBJECTIVES
min fix_cost: nb_year * (yearly_capex + fom) * new_capacity + yearly_existing_cost;
min var_cost: vom * methanol_produced[t];
// max co2_capt_cost: co2_emission_cost * co2_consumed[t];          // M€  



#NODE METHANOL_LIQ_STORAGE
// In GW
#PARAMETERS
pre_installed_capacity_stock = 0;
pre_installed_capacity_flow = 0;
max_capacity_stock = 100;
max_capacity_flow = 1/5 * max_capacity_stock;
lhv_methanol = 5.51; // MWh/tonne
capex_stock_existing = 2.778/lhv_methanol;
capex_flow_existing = 0.0625/lhv_methanol;
fom_stock_existing = 0.0;
fom_flow_existing = 0.0;
vom_stock_existing = 0.0;
vom_flow_existing = 0.0;
lifetime_stock_existing = 30;
lifetime_flow_existing = 30;
capex_stock = 2.778/lhv_methanol;
capex_flow = 0.0625/lhv_methanol;
fom_stock = 0.0;
fom_flow = 0.0;
vom_stock = 0.0;
vom_flow = 0.0;
lifetime_stock = 30;
lifetime_flow = 30;
wacc =0.07;
nb_year = T/8760;
yearly_capex_stock = capex_stock * wacc / (1 - (1 + wacc)**(-lifetime_stock));
yearly_capex_flow = capex_flow * wacc / (1 - (1 + wacc)**(-lifetime_flow));
yearly_capex_stock_existing = capex_stock_existing * wacc / (1 - (1 + wacc)**(-lifetime_stock_existing));
yearly_capex_flow_existing = capex_flow_existing * wacc / (1 - (1 + wacc)**(-lifetime_flow_existing));
yearly_existing_cost = (yearly_capex_stock_existing + fom_stock_existing) * pre_installed_capacity_stock
                     + (yearly_capex_flow_existing + fom_flow_existing) * pre_installed_capacity_flow;

#VARIABLES
internal: capacity_flow;
internal: capacity_stock;
internal: methanol_stored[T];
external: methanol_charged[T];
external: methanol_discharged[T];

#CONSTRAINTS
capacity_stock + pre_installed_capacity_stock <= max_capacity_stock;
capacity_flow + pre_installed_capacity_flow <= max_capacity_flow;
methanol_charged[t] <= capacity_flow + pre_installed_capacity_flow;
methanol_discharged[t] <= capacity_flow + pre_installed_capacity_flow;
methanol_stored[t] <= capacity_stock + pre_installed_capacity_stock;
methanol_stored[0] == methanol_stored[T-1] + methanol_charged[T-1] - methanol_discharged[T-1];
methanol_stored[i+1] == methanol_stored[i] + methanol_charged[i] - methanol_discharged[i] for i in [0:T-2]; 
capacity_flow >= 0;
capacity_stock >= 0;
methanol_stored[t] >= 0;
methanol_charged[t] >= 0;
methanol_discharged[t] >= 0;

#OBJECTIVES
min fix_cost: nb_year * (yearly_capex_stock + fom_stock) * capacity_stock
             + nb_year * (yearly_capex_flow + fom_flow) * capacity_flow + yearly_existing_cost;
min var_cost: vom_stock * (methanol_stored[t]) + vom_flow * (methanol_charged[t]); 