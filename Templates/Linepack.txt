#TIMEHORIZON
T = 8760;

#NODE LINEPACK 
// According to Fluxys, Belgium gas TSO, Belgium has a total of 3.5 millions m³ of natural gas of capacity in their pipeline 

#PARAMETERS
unit_GWh = 0; 
lhv_m3 = 9.944;                                         // GWh(ch4)/millions of m³
pre_installed_capacity = 3.5 * lhv_m3;                  // GWh(ch4)
charge_efficiency = 1;
discharge_efficiency = 1;
self_discharge = 1;
duration_ratio = 0.25;
minimal_level = 0.0;                                    // GWh(ch4)

#VARIABLES
external: state_of_charge[T];                           // GWh(ch4)
external: ng_discharged[T];                                // GWh(ch4) 
external: ng_charged[T];                                   // GWh(ch4) 
external: daily_use[365];                               // GWh(ch4)/day

#CONSTRAINTS
state_of_charge[t] >= 0;
ng_charged[t] >= 0;
ng_discharged[t] >= 0;
state_of_charge[i] == pre_installed_capacity for i in [6:24:T-1];
daily_use[(t-mod(t,24))/24] == sum(ng_charged[i] for i in [t:t+23]) where mod(t,24) == 6;
daily_use[i] <= pre_installed_capacity for i in [0:364];
ng_charged[t] <= duration_ratio * pre_installed_capacity;
ng_discharged[t] <= duration_ratio * pre_installed_capacity;
state_of_charge[t] <= pre_installed_capacity;
state_of_charge[t] >= minimal_level;
state_of_charge[i+1] == self_discharge * state_of_charge[i] + charge_efficiency * ng_charged[i] - 1/discharge_efficiency * ng_discharged[i] for i in [0:T-2];
state_of_charge[0] == self_discharge * state_of_charge[T-1] + charge_efficiency * ng_charged[T-1] - 1/discharge_efficiency * ng_discharged[T-1];