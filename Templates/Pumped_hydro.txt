#TIMEHORIZON
T = 8760 * 1;

#NODE PUMPED_HYDRO
// ASSET data for 2050 and 2015 for cost and ens.dk for efficiency parameters


#PARAMETERS
unit_power_GW = 0;
unit_energy_GWh = 0;
energy_electricity = 0;
pre_installed_capacity_power = 1.3;                                 // GW(e)
pre_installed_capacity_energy = 5.3;                                // GWh(e)
max_capacity_power = 1.3;                                           // GW(e)
max_capacity_energy = 5.3;                                          // GWh(e)
// data from ASSET 2015
capex_power_existing = 0;                                           // M€/GW(e)
capex_energy_existing = 0;                                          // M€/GWh(e) 
fom_power_existing = 22.5;                                          // M€/GW(e)/yr
fom_energy_existing = 0;                                            // M€/GWh(e)/yr
vom_power_existing = 0;                                             // M€/GWh(e) of energy e_charged
vom_energy_existing = 0;                                            // M€/GWh(e) of energy stored
// data from ASSET 2050
capex_power = 0;                                                    // M€/GW(e)
capex_energy = 0;                                                   // M€/GWh(e)
fom_power = 20.0;                                                   // M€/GW(e)/yr
fom_energy = 0;                                                     // M€/GWh(e)/yr
vom_power = 0;                                                      // M€/GWh(e) of energy stored
vom_energy = 0;                                                     // M€/GWh(e) of energy e_charged
// data from ens.dk 2020
lifetime_power_existing = 50;                                       // years
lifetime_energy_existing = 50;                                      // years
roundtrip_efficiency_existing = 0.8;  
charge_efficiency_existing = roundtrip_efficiency_existing**(1/2);
discharge_efficiency_existing = charge_efficiency_existing;
min_SOC_existing = 0.0;                                              
self_discharge_existing = 1;                                   
planned_outage_existing = 0.0;                                        
max_charged_existing = 1;                                           // fraction of the power capacity that limit the amount of electricity that can be e_charged in 1 hour
//data from ens.dk 2050
lifetime_power = 50;                                                // years
lifetime_energy = 50;                                               // years
roundtrip_efficiency = 0.8;  
charge_efficiency = roundtrip_efficiency**(1/2);
discharge_efficiency = charge_efficiency;
min_SOC = 0.0; 
self_discharge = 1;                                                  
planned_outage = 0.0;                                                 
max_charged = 1;                                                    // fraction of the power capacity that limit the amount of electricity that can be e_charged in 1 hour
// Other
nb_year = T/8760;
// yearly capex
wacc = 0.07;
yearly_capex_power = capex_power * wacc / (1 - (1 + wacc)**(-lifetime_power));                                          // M€/GW(e)/yr
yearly_capex_energy = capex_energy * wacc / (1 - (1 + wacc)**(-lifetime_energy));                                       // M€/GWh(e)/yr
// used to compute the total cost of the system after the optimisation
yearly_capex_power_existing = capex_power_existing * wacc / (1 - (1 + wacc)**(-lifetime_power_existing));               // M€/GW(e)/yr
yearly_capex_energy_existing = capex_energy_existing * wacc / (1 - (1 + wacc)**(-lifetime_energy_existing));            // M€/GWh(e)/yr
yearly_existing_cost = (yearly_capex_power_existing + fom_power_existing) * pre_installed_capacity_power 		          
                     + (yearly_capex_energy_existing + fom_energy_existing) * pre_installed_capacity_energy; 	        // M€/yr

#VARIABLES
internal: new_energy_capacity;                                      // GWh(e)
internal: new_power_capacity;                                       // GW(e)
external: e_charged[T];                                               // GWh(e) - e_charged
external: e_discharged[T];                                            // GWh(e) - e_discharged
internal: state_of_charge[T];                                       // GWh(e)

#CONSTRAINTS
new_energy_capacity >= 0;
new_energy_capacity <= max_capacity_energy - pre_installed_capacity_energy;
new_power_capacity >= 0;
new_power_capacity <= max_capacity_power - pre_installed_capacity_power;
e_charged[t] >= 0;
e_discharged[t] >= 0;
state_of_charge[t] >= 0;
state_of_charge[0] == self_discharge * state_of_charge[T-1] + charge_efficiency * e_charged[T-1] - 1/discharge_efficiency * e_discharged[T-1];
min_SOC * (pre_installed_capacity_energy + new_energy_capacity) <= state_of_charge[t];
state_of_charge[t] <= (pre_installed_capacity_energy + new_energy_capacity);
state_of_charge[i+1] == self_discharge * state_of_charge[i] + charge_efficiency * e_charged[i] - 1/discharge_efficiency * e_discharged[i] for i in [0:T-2];
e_discharged[t] <= pre_installed_capacity_power + new_power_capacity;
charge_efficiency * e_charged[t] <= max_charged * (pre_installed_capacity_power + new_power_capacity);
sum(e_charged[i] for i in [0:T-1]) <= (1 - planned_outage) * new_power_capacity * T;
sum(e_discharged[i] for i in [0:T-1]) <= (1 - planned_outage) * new_power_capacity * T;

#OBJECTIVES
min fix_cost_power: (yearly_capex_power + fom_power) * nb_year * new_power_capacity;                                    // M€
min fix_cost_energy: (yearly_capex_energy + fom_energy) * nb_year * new_energy_capacity;                                // M€
min var_cost_power: vom_power * e_charged[t];                                                                             // M€
min var_cost_energy: vom_energy * state_of_charge[t];                                                                   // M€